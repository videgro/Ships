plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'  // Google Services plugin
    id 'com.google.firebase.crashlytics'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    namespace 'net.videgro.ships'

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "net.videgro.ships"
        minSdkVersion 26
        multiDexEnabled true

        // TODO: Update this only when you have a real device available for development and testing.
        targetSdkVersion 33
        compileSdk 34

        versionCode 408
        versionName "4.08"

        externalNativeBuild {
            cmake {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }

    buildTypes {
        release {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }
        }
        debug {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }
        }
    }

    // Upload Crashlytics NDK symbols
    tasks.configureEach { task -> if (task.name.startsWith('assemble') && task.name.endsWith("Release")) task.finalizedBy "uploadCrashlyticsSymbolFile" + task.name.substring('assemble'.length()) }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        viewBinding true
        dataBinding = true
        buildConfig true
    }

    ndkVersion '26.2.11394342'
}

dependencies {
    /* Permissions dispatcher */
    /* https://github.com/permissions-dispatcher/PermissionsDispatcher */
    def versionPermissionsDispatcher = '4.9.2'
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:${versionPermissionsDispatcher}"
    annotationProcessor "com.github.permissions-dispatcher:permissionsdispatcher-processor:${versionPermissionsDispatcher}"
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:${versionPermissionsDispatcher}"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.1"

    /* Firebase */
    implementation platform('com.google.firebase:firebase-bom:32.8.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'
    implementation 'com.google.firebase:firebase-messaging'

    implementation 'com.google.android.gms:play-services-ads:23.0.0'
    implementation 'com.google.android.gms:play-services-wearable:18.1.0'
    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.ump:user-messaging-platform:2.2.0'

    /* Android support libs */
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.media:media:1.7.0'

    /* ARCore */
    implementation 'com.google.ar:core:1.42.0'
    implementation 'com.google.ar.sceneform:core:1.17.1'
    implementation 'com.github.appoly:ARCore-Location:1.2'

    /* Glide - An image loading and caching library for Android - https://github.com/bumptech/glide */
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    /* Misc. */
    implementation 'com.google.code.gson:gson:2.10'
    implementation "org.nanohttpd:nanohttpd:2.3.1"
    implementation "de.codecrafters.tableview:tableview:2.8.1"
    implementation 'com.android.volley:volley:1.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
}
